// Copyright 2023 Cerebras Systems.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

param memcpy_params: comptime_struct;

param peId: u16;

// Colors
param exchColor:        color;
param resultColor:      color;

// Task IDs
param main_task_id: local_task_id;

// ----------
// Every PE needs to import memcpy module otherwise the I/O cannot
// propagate the data to the destination.

// memcpy module reserves input queue 0 and output queue 0
const sys_mod = @import_module( "<memcpy/memcpy>", memcpy_params);
// ----------

/// Helper function to pack 16-bit index and 16-bit float value into one 32-bit
/// wavelet.
fn pack(index: u16, data: f16) u32 {
  return (@as(u32, index) << 16) | @as(u32, @bitcast(u16, data));
}

const size = 12;
const data = [size]u32 {
  pack(0, 10.0),  pack( 1, 11.0), pack( 2, 12.0),
  pack(3, 13.0),  pack( 4, 14.0), pack( 5, 15.0),
  pack(6, 16.0),  pack( 7, 17.0), pack( 8, 18.0),
  pack(9, 19.0),  pack(10, 20.0), pack(11, 21.0),
};

/// Function to send all data values to all east neighbors.
fn sendDataToEastTiles() void {
  const inDsd = @get_dsd(mem1d_dsd, .{
    .tensor_access = |i|{size} -> data[i]
  });

  const outDsd = @get_dsd(fabout_dsd, .{
    .extent = size,
    .fabric_color = exchColor,
    .output_queue = @get_output_queue(2)
  });

  // WARNING: "async" is necessary otherwise CE has no resource
  // to run memcpy kernel
  @mov32(outDsd, inDsd, .{.async=true});
}

/// Function to process (divide by 2) the first three values and send result to
/// the north neighbor (halo PE).
const num_wvlts: u16 = 3;
var buf = @zeros([num_wvlts]f16);
var ptr_buf : [*]f16 = &buf;

fn processAndSendSubset() void {
  const outDsd = @get_dsd(fabout_dsd, .{
    .extent = num_wvlts,
    .fabric_color = resultColor,
    .output_queue = @get_output_queue(1)
  });
  const bufDsd = @get_dsd(mem1d_dsd, .{
    .tensor_access = |i|{num_wvlts} -> buf[i]
  });

  var idx: u16 = 0;
  while (idx < num_wvlts) : (idx += 1) {
    const payload = @as(u16, data[idx] & 0xffff);
    const floatValue = @bitcast(f16, payload);
    buf[idx] = floatValue / 2.0;
  }
  // WARNING: nonblock is necessary otherwise CE has no resource
  // to run memcpy kernel
  @fmovh(outDsd, bufDsd, .{.async = true});
}

task mainTask() void {
  sendDataToEastTiles();
  processAndSendSubset();
}

comptime {
  @activate(main_task_id);
  @bind_local_task(mainTask, main_task_id);

  @set_local_color_config(exchColor, .{ .routes = .{ .rx = .{ RAMP }, .tx = .{ EAST } } });
  @set_local_color_config(resultColor, .{ .routes = .{ .rx = .{ RAMP }, .tx = .{ NORTH } } });
}
