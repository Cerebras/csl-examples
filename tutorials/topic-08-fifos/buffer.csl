// Copyright 2023 Cerebras Systems.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

param memcpy_params: comptime_struct;

param num_elements_to_process: i16;

// Colors
param in_color:         color;
param out_color:        color;
param result_color:     color;

// Task IDs
param process_task_id: data_task_id;  // Data task process_task triggered by out_color wlts
param main_task_id:    local_task_id;

// ----------
// Every PE needs to import memcpy module otherwise the I/O cannot
// propagate the data to the destination.

// memcpy module reserves input queue 0 and output queue 0
const sys_mod = @import_module( "<memcpy/memcpy>", memcpy_params);
// ----------

var fifo_buffer = @zeros([1024]f16);
const fifo = @allocate_fifo(fifo_buffer);

const in_queue = @get_input_queue(0);
const in_dsd = @get_dsd(fabin_dsd, .{.extent = num_elements_to_process,
                                     .fabric_color = in_color,
                                     .input_queue = in_queue});
comptime {
  @set_local_color_config(in_color, .{.routes = .{.rx = .{WEST}, .tx = .{RAMP}}});
}

const out_queue = @get_output_queue(1);
const out_dsd = @get_dsd(fabout_dsd, .{.extent = num_elements_to_process,
                                       .fabric_color = out_color,
                                       .output_queue = out_queue});

const ten = [1]f16 {10.0};
const dsd_ten = @get_dsd(mem1d_dsd, .{.tensor_access = |i|{num_elements_to_process} -> ten[0]});

task main_task() void {
  // Move from the fabric to the FIFO
  // adding 10.0 to each element at the same time
  @faddh(fifo, in_dsd, dsd_ten, .{.async = true});

  // Move from the FIFO to a process_task
  // negating values at the same time
  @fnegh(out_dsd, fifo, .{.async = true});
}

const result_dsd = @get_dsd(fabout_dsd, .{.extent = 1, .fabric_color = result_color});

task process_task(element:f16) void {
  @fmovh(result_dsd, element * element * element);
}

comptime {
  @bind_data_task(process_task, process_task_id); // data task receives wlts along out_color
  @bind_local_task(main_task, main_task_id);
  @activate(main_task_id);

  @set_local_color_config(out_color, .{.routes = .{.rx = .{RAMP}, .tx = .{RAMP}}});
  @set_local_color_config(result_color, .{.routes = .{.rx = .{RAMP}, .tx = .{EAST}}});
}
