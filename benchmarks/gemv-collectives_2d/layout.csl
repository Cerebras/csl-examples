// Copyright 2022 Cerebras Systems.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

// This does y = Ax + b on a kernel_cols x kernel_rows rectangle of PEs.

// Kernel rectangle of PEs
param kernel_rows : u16; // Height of kernel
param kernel_cols : u16; // Width of kernel

// Global matrix dimensions
param matrix_rows : u16; // Height of matrix
param matrix_cols : u16; // Width of matrix

comptime {
  // Number of matrix rows must be multiple of kernel rectangle height
  @comptime_assert(matrix_rows % kernel_rows == 0);

  // Number of matrix cols must be multiple of kernel rectangle width
  @comptime_assert(matrix_cols % kernel_cols == 0);
}

// Local matrix dimensions
const Mt : u16 = matrix_rows / kernel_rows; // Number of rows per PE
const Nt : u16 = matrix_cols / kernel_cols; // Number of columns per PE

const LAUNCH : color = @get_color(8);
const EXIT : color   = @get_color(9);

const memcpy = @import_module( "<memcpy_multi/get_params>", .{
    .width = kernel_cols,
    .height = kernel_rows
});

const c2d = @import_module("<collectives_2d/params>");

layout {
  @set_rectangle(kernel_cols, kernel_rows);

  var Px: u16 = 0;
  while (Px < kernel_cols) : (Px += 1) {
    var Py: u16 = 0;
    const memcpy_params = memcpy.get_params(Px);
    while (Py < kernel_rows) : (Py += 1) {
      const c2d_params = c2d.get_params(Px, Py, .{
        .x_colors = .{ @get_color(0), @get_color(1), @get_color(2), @get_color(3) },
        .y_colors = .{ @get_color(4), @get_color(5), @get_color(6), @get_color(7) },
      });
      @set_tile_code(Px, Py, "pe.csl", .{ .c2d_params = c2d_params, .memcpy_params = memcpy_params,
        .Mt = Mt, .Nt = Nt, .LAUNCH = LAUNCH, .EXIT = EXIT });
    }
  }

  // export symbol names
  @export_name("A", [*]f32, true);
  @export_name("x", [*]f32, true);
  @export_name("b", [*]f32, true);
  @export_name("y", [*]f32, true);
  @export_name("main", fn()void);
}
